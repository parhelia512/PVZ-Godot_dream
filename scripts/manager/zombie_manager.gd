extends Node2D
class_name ZombieManager

@onready var main_game: MainGameManager = $"../.."
## æœ€å¤§æ³¢æ¬¡
@export var max_wave := 50
var current_wave := 0
var wave_total_health := 0
var wave_current_health := 0
var refresh_threshold := 0.5  # æ³¢æ¬¡è§¦å‘æ¿€æ´»åˆ·æ–°çš„è¡€é‡æµå¤±æ¯”ä¾‹
var refresh_triggered := false
var refresh_health := 0  # æ³¢æ¬¡è§¦å‘æ¿€æ´»åˆ·æ–°çš„è¡€é‡æµå¤±

var curr_flag := -1
var flag_front_wave := false	#æ˜¯å¦ä¸ºæ——å‰æ³¢
@export var curr_zombie_num:int = 0

## æ‰€æœ‰åƒµå°¸è¡Œ
@export var zombies_row:Array
@export var flag_progress_bar: FlagProgressBar
## è‡ªç„¶åˆ·æ–°è®¡æ—¶å™¨
@onready var wave_timer: Timer = $WaveTimer
## æ¯ç§’è¿›åº¦æ¡æ›´æ–°è®¡æ—¶å™¨
@onready var one_wave_progress_timer: Timer = $OneWaveProgressTimer

@onready var label_zombie_sum: Label = $LabelZombieSum

## æ˜¯å¦ä¸ºæœ€åä¸€æ³¢
@export var end_wave:= false
## æ¯æ³¢è¿›åº¦æ¡æ‰€å å¤§å°
@export var progress_bar_segment_every_wave:float

## æ¯æ®µæ ¹æ®å½“å‰æ³¢æ¬¡æ—¶é—´ï¼Œæ¯ç§’å¤šé•¿
@export var progress_bar_segment_mini_every_sec:float

#region åƒµå°¸å‡ºæ€ªåˆ—è¡¨ç›¸å…³å‚æ•°
# åˆ›å»ºå‡ºæ€ªåˆ—è¡¨
var spawn_list = []

# ç”Ÿæˆåƒµå°¸çš„æ³¢æ¬¡ï¼Œæœ€å¤§ä¸º100æ³¢
@export var max_waves_spawn_list = 100
@export var max_zombies_per_wave = 50

# å®šä¹‰æ¯ä¸ªåƒµå°¸çš„æˆ˜åŠ›å€¼
var zombie_power = {
	Global.ZombieType.ZombieNorm: 1,        # æ™®åƒµæˆ˜åŠ›
	Global.ZombieType.ZombieFlag: 1,        # æ——å¸œæˆ˜åŠ›
	Global.ZombieType.ZombieCone: 2,        # è·¯éšœæˆ˜åŠ›
	Global.ZombieType.ZombiePoleVaulter: 2, # æ’‘æ†æˆ˜åŠ›
	Global.ZombieType.ZombieBucket: 4,      # é“æ¡¶æˆ˜åŠ›
	Global.ZombieType.ZombiePaper: 4       # è¯»æŠ¥æˆ˜åŠ›
}

# åˆ›å»º zombie_weights å­—å…¸ï¼Œå­˜å‚¨åˆå§‹æƒé‡
var zombie_weights = {
	Global.ZombieType.ZombieNorm: 4000,        # æ™®åƒµæƒé‡
	Global.ZombieType.ZombieFlag: 0,           # æ——å¸œæƒé‡
	Global.ZombieType.ZombieCone: 4000,        # è·¯éšœæƒé‡
	Global.ZombieType.ZombiePoleVaulter: 2000,  # æ’‘æ†æƒé‡
	Global.ZombieType.ZombieBucket: 3000,       # é“æ¡¶æƒé‡
	Global.ZombieType.ZombiePaper: 3000       # è¯»æŠ¥æƒé‡
}


## **ç»Ÿä¸€çš„åƒµå°¸ç§ç±»åˆ·æ–°åˆ—è¡¨**ï¼Œè¿™å°†å®šä¹‰æ•´å±€æ¸¸æˆæ¯æ³¢å¯ä»¥åˆ·æ–°çš„åƒµå°¸ç§ç±»
@export var zombie_refresh_types : Array[Global.ZombieType] = [
	Global.ZombieType.ZombieNorm,       # æ™®é€šåƒµå°¸
	#Global.ZombieType.ZombieFlag,       # æ——å¸œåƒµå°¸
	Global.ZombieType.ZombieCone,       # è·¯éšœåƒµå°¸
	Global.ZombieType.ZombiePoleVaulter, # æ’‘æ†åƒµå°¸
	Global.ZombieType.ZombieBucket,      # é“æ¡¶åƒµå°¸
	Global.ZombieType.ZombiePaper      # è¯»æŠ¥åƒµå°¸
	
]
#endregion

#region å…³å¡å‰å±•ç¤ºåƒµå°¸
@export_group("å±•ç¤ºåƒµå°¸ç›¸å…³")
@onready var show_zombie: Node2D = $"../../ShowZombie"
@export var show_zombie_pos_start := Vector2(50, 50)
@export var show_zombie_pos_end := Vector2(250, 450)
@export var show_zombie_array : Array[ZombieBase] = []

#endregion

## å¥–æ¯
const trophy_scenes = preload("res://scenes/ui/trophy.tscn")

# ç”Ÿæˆ100æ³¢å‡ºæ€ªåˆ—è¡¨ï¼Œæ¯æ³¢æœ€å¤š50åªåƒµå°¸
func _ready():
	
	create_spawn_list()
	# ä¸ç›´æ¥è°ƒç”¨åˆå§‹åŒ–ï¼Œå»¶è¿Ÿè°ƒç”¨
	call_deferred("_init_var")
	
	#show_zombie_create()

func _init_var():
	zombies_row = get_tree().root.get_node("MainGame/Zombies").get_children()
	flag_progress_bar = get_tree().root.get_node("MainGame/FlagProgressBar")
	flag_progress_bar.init_flag_from_wave(max_wave)

	progress_bar_segment_every_wave = 100.0 / (max_wave - 1)
	
#region ç”Ÿæˆåƒµå°¸åˆ—è¡¨

func create_spawn_list():
	"""
	å¤§æ³¢ï¼ˆæ¯10æ³¢ä¸€æ¬¡ï¼‰ä¼šä¼˜å…ˆç”Ÿæˆä¸€å®šæ•°é‡çš„ç‰¹æ®Šåƒµå°¸ï¼ˆæ——å¸œåƒµå°¸å’Œæ™®é€šåƒµå°¸ï¼‰ï¼Œä¸”æˆ˜åŠ›ä¸Šé™æ˜¯æ™®é€šæ³¢çš„2.5å€ã€‚
	æ™®é€šæ³¢çš„åƒµå°¸ç±»å‹å’Œæ•°é‡æ˜¯æ ¹æ®æƒé‡è¿›è¡Œéšæœºç”Ÿæˆçš„ã€‚
	æƒé‡é€’å‡æœºåˆ¶ï¼šéšç€æ³¢æ¬¡çš„å¢åŠ ï¼Œæ™®é€šåƒµå°¸å’Œè·¯éšœåƒµå°¸çš„æƒé‡ä¼šé€æ¸ä¸‹é™ï¼Œç›´åˆ°å›ºå®šå€¼ï¼ˆç¬¬25æ³¢ä»¥åï¼‰
	"""
	for wave_index in range(max_waves_spawn_list):
		var wave_spawn = []  # å½“å‰æ³¢çš„åƒµå°¸åˆ—è¡¨
		var remaining_slots = max_zombies_per_wave
		
		# åˆ¤æ–­æ˜¯å¦ä¸ºå¤§æ³¢
		var is_big_wave = (wave_index + 1) % 10 == 0
		# è®¡ç®—å½“å‰æ³¢çš„æˆ˜åŠ›ä¸Šé™
		var current_power_limit = calculate_wave_power_limit(wave_index + 1, is_big_wave)

		# å¦‚æœæ˜¯å¤§æ³¢ï¼Œå…ˆåˆ·æ–°ç‰¹æ®Šåƒµå°¸
		var total_power = 0
		if is_big_wave:
			# ç¬¬ä¸€ä¸ªæ——å¸œåƒµå°¸
			wave_spawn.append(Global.ZombieType.ZombieFlag)
			total_power += zombie_power[Global.ZombieType.ZombieFlag]
			remaining_slots -= 1
			
			# ç¬¬ä¸€æ¬¡å¤§æ³¢ï¼ˆç¬¬10æ³¢ï¼‰ï¼Œåˆ·æ–°4ä¸ªæ™®é€šåƒµå°¸
			if wave_index == 9:
				for i in range(4):
					wave_spawn.append(Global.ZombieType.ZombieNorm)
					total_power += zombie_power[Global.ZombieType.ZombieNorm]
					remaining_slots -= 1
			# ä¹‹åçš„å¤§æ³¢ï¼ˆç¬¬20æ³¢ã€30æ³¢...ï¼‰ï¼Œåˆ·æ–°8ä¸ªæ™®é€šåƒµå°¸
			else:
				for i in range(8):
					wave_spawn.append(Global.ZombieType.ZombieNorm)
					total_power += zombie_power[Global.ZombieType.ZombieNorm]
					remaining_slots -= 1

		# åŠ¨æ€è°ƒæ•´æ™®é€šåƒµå°¸å’Œè·¯éšœåƒµå°¸çš„æƒé‡
		update_weights(wave_index)

		# ç”Ÿæˆå‰©ä½™åƒµå°¸ï¼Œç›´åˆ°æ€»æˆ˜åŠ›ç¬¦åˆå½“å‰æˆ˜åŠ›ä¸Šé™
		while remaining_slots > 0 and total_power < current_power_limit:
			var selected_zombie = get_random_zombie_based_on_weight()
			var zombie_power_value = zombie_power[selected_zombie]
			
			# æ£€æŸ¥å¦‚æœåŠ ä¸Šè¯¥åƒµå°¸çš„æˆ˜åŠ›åè¶…è¿‡å½“å‰æ³¢çš„æˆ˜åŠ›ä¸Šé™ï¼Œé‡æ–°é€‰æ‹©
			if total_power + zombie_power_value <= current_power_limit:
				wave_spawn.append(selected_zombie)
				total_power += zombie_power_value
				remaining_slots -= 1
			else:
				continue
		
		# å°†å½“å‰æ³¢çš„åƒµå°¸åˆ—è¡¨æ·»åŠ åˆ°å‡ºæ€ªåˆ—è¡¨ä¸­
		spawn_list.append(wave_spawn)
		
		#print(wave_index, " ", current_power_limit, " ", wave_spawn)
	return spawn_list

# è®¡ç®—æ¯æ³¢çš„æˆ˜åŠ›ä¸Šé™
func calculate_wave_power_limit(wave_index:int, is_big_wave: bool) -> int:
	# è®¡ç®—æˆ˜åŠ›ä¸Šé™ = y=int((x-1)/3)+1
	var base_power_limit:int = (wave_index - 1) / 3 + 1
	
	# å¦‚æœæ˜¯å¤§æ³¢ï¼Œæˆ˜åŠ›ä¸Šé™æ˜¯åŸæˆ˜åŠ›ä¸Šé™çš„2.5å€
	if is_big_wave:
		return int(base_power_limit * 2.5)
	
	return base_power_limit


# è·å–æ ¹æ®æƒé‡é€‰æ‹©çš„åƒµå°¸
func get_random_zombie_based_on_weight() -> int:
	# åŸºäºç»Ÿä¸€çš„åˆ·æ–°ç§ç±»éšæœºé€‰æ‹©åƒµå°¸
	var cumulative_weight = 0
	var max_weight = 0
	
	# è®¡ç®—æ‰€æœ‰å¯èƒ½åƒµå°¸çš„æƒé‡æ€»å’Œ
	for zombie_type in zombie_refresh_types:
		max_weight += zombie_weights[zombie_type]

	var random_value = randi_range(0, max_weight)  # ä½¿ç”¨åŠ¨æ€è®¡ç®—çš„æœ€å¤§æƒé‡

	for zombie_type in zombie_refresh_types:
		cumulative_weight += zombie_weights[zombie_type]
		
		if random_value < cumulative_weight:
			return zombie_type  # è¿”å›é€‰ä¸­çš„åƒµå°¸ç±»å‹

	# å¦‚æœæ²¡æœ‰é€‰ä¸­ï¼Œé»˜è®¤è¿”å›æ™®é€šåƒµå°¸
	return Global.ZombieType.ZombieNorm


# æ›´æ–°åƒµå°¸æƒé‡
func update_weights(wave_index: int):
	# æ™®é€šåƒµå°¸æƒé‡è°ƒæ•´é€»è¾‘
	if wave_index >= 5:
		var norm_weight = 4000 - (wave_index - 4) * 180
		if wave_index >= 25:
			norm_weight = 400  # å½“è½®æ¬¡è¾¾åˆ°25æ—¶ï¼Œæ™®é€šåƒµå°¸æƒé‡å›ºå®šä¸º400
		zombie_weights[Global.ZombieType.ZombieNorm] = norm_weight

	# è·¯éšœåƒµå°¸æƒé‡è°ƒæ•´é€»è¾‘
	if wave_index >= 5:
		var cone_weight = 4000 - (wave_index - 4) * 150
		if wave_index >= 25:
			cone_weight = 1000  # å½“è½®æ¬¡è¾¾åˆ°25æ—¶ï¼Œè·¯éšœåƒµå°¸æƒé‡å›ºå®šä¸º1000
		zombie_weights[Global.ZombieType.ZombieCone] = cone_weight
	
#endregion

#region åƒµå°¸åˆ·æ–°

"""

é™¤äº†W9, W19å’ŒW20 å¤–, å¯¹åˆ·æ–°å‡ºæ¥çš„æ¯æ³¢åƒµå°¸ï¼Œå­˜åœ¨ä¸€ä¸ª0.5~0.67ä¹‹é—´çš„å€¼kï¼Œ
è®°åˆ·æ–°ä¹‹åtsæ—¶åœºä¸Šè¯¥æ³¢åƒµå°¸æ€»è¡€é‡å¼€å§‹å°äºæ€»è¡€é‡çš„kå€( ç§°ä¸ºæ¿€æ´» )ï¼Œåˆ™å­˜åœ¨ä¸€ä¸ªåŒºé—´[25,31]å†…çš„éšæœºæ•°T(è‡ªç„¶åˆ·æ–°)ï¼Œ
åœ¨åˆ·æ–°ä¹‹åmin{T,max{6.01,t+2}}såˆ·æ–°ä¸‹ä¸€æ³¢åƒµå°¸; å¯¹W9å’ŒW19, è®°åˆ·æ–°ä¹‹åtsæ—¶åœºä¸Šè¯¥æ³¢åƒµå°¸( é™¤ä¼´èˆ )å…¨éƒ¨æ­»äº¡,
åˆ™åœ¨åˆ·æ–°ä¹‹åmin{52.45, max{6.01+7.5, t+2+7.5}}såˆ·æ–°ä¸‹ä¸€æ³¢åƒµå°¸(åŒ…æ‹¬å…¶ä¸­çš„7.5sçº¢å­—æ—¶é—´); 
å¯¹W20, è®°åˆ·æ–°ä¹‹åtsæ—¶åœºä¸Šåƒµå°¸å…¨éƒ¨æ­»äº¡, åˆ™åœ¨åˆ·æ–°ä¹‹åmin{60, max{6.01+5, t+2+5}}såè¿›å…¥ä¸‹ä¸€æ¬¡é€‰å¡. è¿™ä¸ªæ—¶é—´ç§°ä¸ºè¯¥æ³¢çš„æ³¢é•¿.

"""

## è®¡ç®—å½“å‰è¿›åº¦å¹¶æ›´æ–°è¿›åº¦æ¡
func set_progress_bar():
	var curr_progress = current_wave * progress_bar_segment_every_wave
	flag_progress_bar.set_progress(curr_progress, curr_flag)
	
func start_first_wave():
	start_next_wave()
	one_wave_progress_timer.start()


## å¼€å§‹åˆ·æ–°ä¸‹ä¸€æ³¢
func start_next_wave() -> void:
	print("-----------------------------------")
	if current_wave >= max_wave:
		print("ğŸ‰ ç»“æŸ(è¯¥è¯­å¥åº”è¯¥ä¸å‡ºç°é€»è¾‘æ‰å¯¹)")
		return
	
	spawn_wave_zombies(spawn_list[current_wave])
	
	refresh_triggered = false
	end_wave = current_wave == max_wave - 1
	if end_wave:
		print("æœ€åä¸€æ³¢")
		wave_timer.stop()
		one_wave_progress_timer.stop()
		return 
		
	start_natural_refresh_timer()
	
	## æ®‹åŠåˆ·æ–°è¡€é‡å€ç‡
	refresh_threshold = randf_range(0.5, 0.67)
	refresh_health = int(refresh_threshold * wave_total_health)
	print("ğŸŒ€ ç¬¬ %d æ³¢å¼€å§‹ï¼Œåˆ·æ–°é˜ˆå€¼è®¾ä¸º %.2fï¼Œåˆ·æ–°è¡€é‡ä¸º %dï¼Œè‡ªç„¶åˆ·æ–°æ—¶é—´ä¸º %f" % [current_wave, refresh_threshold, refresh_health, wave_timer.wait_time])
	
#region ç”Ÿæˆæ³¢æ¬¡åƒµå°¸
## ç”Ÿæˆå½“å‰æ³¢æ¬¡åƒµå°¸
func spawn_wave_zombies(zombie_data: Array) -> void:
	# æ›´æ–°å½“å‰æ³¢æ¬¡åƒµå°¸æ€»è¡€é‡
	wave_total_health = 0
	wave_current_health = 0
	
	for z in zombie_data:
		var lane : int = randi() % len(zombies_row)
		var zombie:ZombieBase = spawn_zombie(z, lane)
		zombie.lane = lane
		zombie.zombie_damaged.connect(_on_zombie_damaged)
		zombie.zombie_dead.connect(_on_zombie_dead)
		zombie.curr_wave = current_wave
		zombie.is_idle = false
		if zombie.zombie_type == Global.ZombieType.ZombieFlag:
			print("æ——å¸œåƒµå°¸")
			zombie.position.x = -20
		else:
			zombie.position.x = randf_range(0, 20)
		
		wave_total_health += zombie.get_zombie_all_hp()
	wave_current_health = wave_total_health
	label_zombie_sum.text = "å½“å‰åƒµå°¸æ•°é‡ï¼š" + str(curr_zombie_num)
	
## ç”Ÿæˆä¸€ä¸ªåƒµå°¸
func spawn_zombie(zombie_type: Global.ZombieType, lane: int) -> Node:
	var z:ZombieBase = Global.ZombieTypeSceneMap[zombie_type].instantiate()
	zombies_row[lane].add_child(z)
	curr_zombie_num += 1
	
	return z
	
#endregion

## åƒµå°¸æ”¶åˆ°ä¼¤å®³è°ƒç”¨å‡½æ•°
func _on_zombie_damaged(damage: int, wave:int) -> void:
	# ä¸æ˜¯æœ€åä¸€æ³¢
	if wave == current_wave and not end_wave:
		wave_current_health = max(wave_current_health - damage, 0)
		check_refresh_condition()

## åƒµå°¸å‘å°„æ­»äº¡ä¿¡å·åè°ƒç”¨å‡½æ•°
func _on_zombie_dead(zombie_global_position: Vector2) -> void:
	# ä¸é¢å¤–å‡è¡€ï¼›æ­»äº¡å‰å·²ç”± take_damage æ‰£å‡
	curr_zombie_num -= 1
	label_zombie_sum.text = "å½“å‰åƒµå°¸æ•°é‡ï¼š" + str(curr_zombie_num)
	
	## å½“å‰æ˜¯æ——å‰æ³¢å¹¶åƒµå°¸å…¨éƒ¨æ­»äº¡
	if flag_front_wave and curr_zombie_num == 0:
		
		refresh_flag_wave()
	
	# å¦‚æœåˆ°äº†æœ€åä¸€æ³¢åˆ·æ–°,ä¸”åƒµå°¸å…¨éƒ¨æ­»äº¡
	if end_wave and curr_zombie_num == 0 :
		print("=======================æ¸¸æˆç»“æŸï¼Œæ‚¨è·èƒœäº†=======================")
		var trophy = trophy_scenes.instantiate()
		get_tree().current_scene.add_child(trophy)
		trophy.global_position = zombie_global_position
		if trophy.global_position.x >= 750:
			var x_diff = trophy.global_position.x - 750
			throw_to(trophy, trophy.position - Vector2(x_diff + randf_range(0,50), 0))
		else:
			throw_to(trophy, trophy.position - Vector2(randf_range(-50,50), 0))
		
func throw_to(node:Node2D, target_pos: Vector2, duration: float = 1.0):
	
	var start_pos = node.position
	var peak_pos = start_pos.lerp(target_pos, 0.5)
	peak_pos.y -= 50  # å‘ä¸ŠæŠ›

	var tween = create_tween()
	tween.tween_property(node, "position:x", target_pos.x, duration).set_trans(Tween.TRANS_LINEAR)

	tween.parallel().tween_property(node, "position:y", peak_pos.y, duration / 2).set_trans(Tween.TRANS_SINE).set_ease(Tween.EASE_OUT)
	tween.parallel().tween_property(node, "position:y", target_pos.y, duration / 2).set_trans(Tween.TRANS_SINE).set_ease(Tween.EASE_IN).set_delay(duration / 2)



# åƒµå°¸æ‰£è¡€æ—¶æ£€æµ‹åˆ·æ–°çŠ¶æ€
func check_refresh_condition() -> void:
	if refresh_triggered:
		return
		
	# ä¸æ˜¯æ——å‰æ³¢è¿›è¡Œæ®‹åŠåˆ·æ–°åˆ¤æ–­
	if not flag_front_wave and wave_current_health <= refresh_health:
		refresh_health_half()
	
		
## æ®‹åŠåˆ·æ–°åˆ¤æ–­
func refresh_health_half():
	if refresh_triggered:
	
		return
	print("âš¡ æ¿€æ´»åˆ·æ–°è¾¾æˆï¼ˆå½“å‰è¡€é‡:,",wave_current_health,"åˆ·æ–°è¡€é‡", refresh_health)
	refresh_triggered = true
	if wave_timer.is_stopped() == false:
		wave_timer.stop()
		
	update_current_wave()
	await get_tree().create_timer(2.0).timeout
	start_next_wave()


## æ——å¸œæ³¢åƒµå°¸å…¨éƒ¨æ­»äº¡åˆ·æ–°
func refresh_flag_wave():
	if refresh_triggered:
		print("è¿›æ——å¸œæ³¢åˆ·æ–°æœºåˆ¶ï¼Œæ——å‰æ³¢çš„å‰ä¸€æ³¢æ®‹åŠåˆ·æ–°æ—¶å…¨éƒ¨æ­»äº¡ä¼šè¿›åˆ°è¿™ä¸ªé‡Œé¢ï¼Œæ­¤æ—¶æ­£åœ¨åˆ·æ–°æ——å‰æ³¢ï¼Œç›´æ¥è¿”å›")
		return
	print("âš¡ æ——å¸œæ³¢æå‰åˆ·æ–°")
	refresh_triggered = true
	if wave_timer.is_stopped() == false:
		wave_timer.stop()
		
	update_current_wave()	# æ›´æ–°å½“å‰æ³¢æ¬¡
	print("ç­‰å¾…å¼€å§‹")
	var start_time = Time.get_ticks_msec()
	
	await main_game.ui_remind_word.zombie_approach(current_wave == max_wave-1)
	await get_tree().create_timer(2.0).timeout
	
	var end_time = Time.get_ticks_msec()
	var elapsed = (end_time - start_time) / 1000.0  # è½¬æ¢ä¸ºç§’

	print("ç­‰å¾…ç»“æŸï¼Œè€—æ—¶:", elapsed, "ç§’")
	
	start_next_wave()

#region è‡ªç„¶åˆ·æ–°
## å¼€å§‹è®¡ç®—è‡ªç„¶åˆ·æ–°æ—¶é—´
func start_natural_refresh_timer() -> void:
	# å¦‚æœæ˜¯æ——å‰æ³¢åˆ·æ–°æ—¶é—´å›ºå®š52.45 -7.5
	var interval = randf_range(23.0, 29.0) if not flag_front_wave else 52.45 -7.5
	print("å½“å‰æ³¢æ¬¡ï¼š", current_wave, "æ——å‰æ³¢ï¼š", flag_front_wave, "åˆ·æ–°æ—¶é—´ï¼š",interval)
	wave_timer.wait_time = interval
	wave_timer.start()
	# æ¯æ¬¡æ›´æ–°æ¯ç§’è¿›åº¦æ¡å¤§å°
	if flag_front_wave:
		progress_bar_segment_mini_every_sec = progress_bar_segment_every_wave / (wave_timer.wait_time + 7.5)
	else:
		progress_bar_segment_mini_every_sec = progress_bar_segment_every_wave / (wave_timer.wait_time + 2)
	
## è‡ªç„¶åˆ·æ–°æ—¶é—´è§¦å‘
func _on_WaveTimer_timeout() -> void:
	if refresh_triggered:
		return
	print("âŒ› è‡ªç„¶åˆ·æ–°è§¦å‘")
	update_current_wave()
	## æ——å¸œæ³¢
	if current_wave % 10 == 9:
		await main_game.ui_remind_word.zombie_approach(current_wave == max_wave-1)
		await get_tree().create_timer(2.0).timeout
	else:
		await get_tree().create_timer(2.0).timeout
	refresh_triggered = true
	start_next_wave()
#endregion

## æ›´æ–°å½“å‰wave,åœ¨æ®‹åŠåˆ·æ–°æˆ–è‡ªç„¶åˆ·æ–°æ—¶ä½¿ç”¨
func update_current_wave():
	current_wave += 1
	if current_wave % 10 == 8:
		flag_front_wave = true
		one_wave_progress_timer.stop()	# æ›´æ–°è¿›åº¦æ¡çš„è®¡æ—¶å™¨
	else:
		flag_front_wave = false
		one_wave_progress_timer.start()
	
	## æ——å¸œæ³¢æ›´æ–°ç¬¬å‡ ä¸ªæ——å¸œï¼Œç”¨äºæ›´æ–°è¿›åº¦æ¡æ——å¸œå‡æ——
	if current_wave % 10 == 9:
		curr_flag = current_wave/10
		
	set_progress_bar()
#endregion
	

## éšæ—¶é—´æ›´æ–°è¿›åº¦æ¡
func _on_one_wave_progress_timer_timeout() -> void:
	# æ¯ç§’è¿›åº¦æ¡å¢åŠ 
	flag_progress_bar.set_progress_add_every_sec(progress_bar_segment_mini_every_sec)


#region ç”Ÿæˆå…³å¡å‰å±•ç¤ºåƒµå°¸
func show_zombie_create():
	for zombie_type in zombie_refresh_types:
		for i in range(randi_range(1, 4)):

			var z:ZombieBase = Global.ZombieTypeSceneMap[zombie_type].instantiate()

			z.is_idle = true

			# é¿å…åƒµå°¸ç§»åŠ¨
			z.walking_status = z.WalkingStatus.end
			z.position = Vector2(
				randf_range(show_zombie_pos_start.x, show_zombie_pos_end.x),
				randf_range(show_zombie_pos_start.y, show_zombie_pos_end.y)
			)
			show_zombie.add_child(z)
			
			
			show_zombie_array.append(z)
		
func show_zombie_delete():
	for z in show_zombie_array:
		z.queue_free()  # æ ‡è®°èŠ‚ç‚¹å¾…é‡Šæ”¾

	show_zombie_array.clear()  # æœ€åæ¸…ç©ºæ•°ç»„

#endregion
